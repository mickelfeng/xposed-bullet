apply plugin: 'com.android.application'
apply plugin: 'stringfog'

apply from: "../config.gradle"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.sky.xposed.bullet"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 2
        versionName "1.0.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resValue 'string','app_name','xposed-bullet'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        sky {
            storeFile file("${rootDir}/keyStore/sky.jks")
            storePassword project.storePassword
            keyAlias project.keyAlias
            keyPassword project.keyPassword
        }
    }
    buildTypes {
        release {
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${project.name}_${variant.flavorName}_v${variant.versionName}.apk"
                }
            }
            randomKeywords()

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', "${rootDir}/proguard/proguard-rules.pro"
            signingConfig signingConfigs.sky
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.sky
        }
    }

    flavorDimensions "default"

    productFlavors {
        all { flavor ->
            resValue 'string', 'flavor', flavor.name
        }
        bullet {
            dimension "default"
            resValue 'string','app_name','子弹壳'
        }
    }
}

stringfog {
    key '&*(&*DFJKDF(*FDF'
    enable true
    fogPackages = ['com.sky.xposed.bullet']
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    compileOnly 'de.robv.android.xposed:api:82'
    compileOnly 'com.rover12421.AndroidHideApi:android:1.17'
//    implementation 'org.apache.commons:commons-lang3:3.5'
//    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.tencent.bugly:crashreport:2.6.6'
    implementation 'com.github.sky-wei:xposed-javax:1.0.5'
    implementation 'com.github.sky-wei:xposed-common:1.0.4'
    implementation 'com.github.megatronking.stringfog:lib:1.2.2'
}
